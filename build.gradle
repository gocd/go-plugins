/*
 * Copyright 2021 Thoughtworks, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'maven-publish'
}

group = 'com.thoughtworks.go'
version = '1.0'

static String versionOf(String dependencySpec) {
    return dependencySpec.split(':').last()
}

ext {
    goVersion = '26.1.0'

    deps = [
        assertJ         : 'org.assertj:assertj-core:3.27.7',
        gson            : 'com.google.code.gson:gson:2.13.2',
        jgit            : 'org.eclipse.jgit:org.eclipse.jgit:7.5.0.202512021534-r',
        junit5          : 'org.junit:junit-bom:6.0.2',
        jsonAssert      : 'org.skyscreamer:jsonassert:1.5.3',
        mockito         : 'org.mockito:mockito-bom:5.21.0',
        okHttp          : 'com.squareup.okhttp3:okhttp:5.3.2',
    ]

    versions = project.ext.deps.collectEntries { lib, libGav -> [lib, versionOf(libGav)] }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    group = 'com.thoughtworks.go'
    version = '1.0'

    java {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    tasks.withType(JavaCompile).configureEach {
        options.release = 17
        options.encoding = 'UTF-8'
    }

    repositories {
        mavenLocal()
        mavenCentral()
    }

    dependencies {
        compileOnly "cd.go.plugin:go-plugin-api:${project.goVersion}"

        testImplementation platform(project.deps.junit5)
        testImplementation 'org.junit.jupiter:junit-jupiter-api'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
        testImplementation project.deps.assertJ
        testImplementation platform(project.deps.mockito)
        testImplementation 'org.mockito:mockito-core'
        testImplementation 'org.mockito:mockito-junit-jupiter'
        testImplementation "cd.go.plugin:go-plugin-api:${project.goVersion}"
    }

    jar {
        preserveFileTimestamps = false
        reproducibleFileOrder = true

        manifest {
            attributes "Go-Version": project.goVersion
        }

        archiveFileName = "${project.name}.jar"
    }

    test {
        useJUnitPlatform()
    }
}

tasks.register('copyJarsToOnePlace', Copy) {
    dependsOn subprojects.jar
    from(subprojects.findAll { !['plugin-1', 'plugin-2'].contains(it.name) }.jar)
    into project.file('target/go-plugins-dist/')
}
